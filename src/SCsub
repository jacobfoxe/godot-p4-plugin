#!/usr/bin/env python

import os

env = {}
Import("env")

# Process some arguments
if env["use_llvm"]:
    env["CC"] = "clang"
    env["CXX"] = "clang++"

if env["platform"] == "":
    print("No valid target platform selected.")
    quit()

env["target_path"] = "../" + env["target_path"]

if not os.path.isdir(env["target_path"]):
    os.mkdir(env["target_path"])

# Check our platform specifics
if env["platform"] == "macos":
    env["target_path"] += "macos/"

    # Force static linkage (https://stackoverflow.com/a/2995999/7370948)
    env.Append(LIBS=[File(env["macos_openssl_static_ssl"]),
                     File(env["macos_openssl_static_crypto"])])

    if env["macos_deployment_target"] != "default":
        env.Append(CCFLAGS=["-mmacosx-version-min=" +
                            env["macos_deployment_target"]])
        env.Append(LINKFLAGS=["-mmacosx-version-min=" +
                              env["macos_deployment_target"]])

elif env["platform"] == "linux":
    env["target_path"] += "linux/"
    env.Append(LIBS=[File(env["linux_openssl_static_ssl"]),
                     File(env["linux_openssl_static_crypto"])])

elif env["platform"] == "windows":
    env["target_path"] += "win64/"

    env.Append(LIBS=["advapi32", "winhttp", "rpcrt4", "crypt32",
               "ole32", "user32", "wsock32", "ws2_32", "bcrypt"])

env.Append(CPPPATH=[".", "src/"])

env.Append(CPPPATH=["../util/perforce-api/include/"])
env.Append(LIBPATH=["../util/perforce/"])
env.Append(LIBPATH=["../util/bin/"])
env.Prepend(LIBS=["libclient", "libp4api", "libp4script_c", "libp4script_cstub",
                  "libp4script_curl", "libp4script_sqlite", "libp4script", "librpc",
                  "libsupp"])

library = env.SharedLibrary(
    target=env["target_path"] +
    "{}{}{}".format(env["target_name"], env["suffix"], env["SHLIBSUFFIX"]),
    source=Glob("src/*.cpp")
)
Default(library)
